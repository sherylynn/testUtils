apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 28

    ext.kotlin_version = '1.3.41'
    defaultConfig {
        applicationId "com.sherylynn.testUtils"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    //implementation 'com.android.tools.build:aapt2:3.4.2'

    // Xposed
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly 'de.robv.android.xposed:api:82:sources'
}
repositories {
    mavenCentral()
    jcenter()
}
afterEvaluate {
    installDebug.doLast {
        // 更新 VXP 中的 app
        exec{
            def pkg = android.defaultConfig.applicationId
            commandLine android.adbExecutable, 'shell', 'am', 'broadcast', '-a', 'io.va.exposed.CMD', '-e', 'cmd', 'update', '-e', 'pkg', pkg
        }
        // 重启 VXP
        exec{
            commandLine android.adbExecutable, 'shell', 'am', 'broadcast', '-a', 'io.va.exposed.CMD', '-e', 'cmd', 'reboot'
        }
        // 重启 VXP 需要 hook 的 APP，需要知道它的包名
        exec{
            def pkg = 'com.sherylynn.testUtils'// 需要 hook 的 app ,这里hook自己
            commandLine android.adbExecutable, 'shell', 'am', 'broadcast', '-a', 'io.va.exposed.CMD', '-e', 'cmd', 'launch', '-e', 'pkg', pkg
        }
    }
}
